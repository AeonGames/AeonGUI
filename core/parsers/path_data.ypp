/*
Copyright (C) 2019 Rodrigo Jose Hernandez Cordoba

Licensed under the Apache License, Version 2.0 (the "License");
you may not use this file except in compliance with the License.
You may obtain a copy of the License at

http://www.apache.org/licenses/LICENSE-2.0

Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.
*/
%{
#include <cstddef>
#include <cstdint>
#include <iostream>
extern int dlex();
extern "C"
{
    int derror (const char *s);
}
%}

%define api.prefix {d}

%union {
  double Number;
  uint64_t Command;
}

%token NUMBER

%%

svg-path:
    | moveto-drawto-command-groups
    ;
moveto-drawto-command-groups:
    moveto-drawto-command-group
    | moveto-drawto-command-groups moveto-drawto-command-group;
moveto-drawto-command-group:
    moveto
    | moveto drawto-commands
    ;
drawto-commands:
    drawto-command
    | drawto-commands drawto-command;
drawto-command:
    closepath
    | lineto
    | horizontal-lineto
    | vertical-lineto
    | curveto
    | smooth-curveto
    | quadratic-bezier-curveto
    | smooth-quadratic-bezier-curveto
    | elliptical-arc;
moveto:
    'M' moveto-argument-sequence
    | 'm'  moveto-argument-sequence
    ;
moveto-argument-sequence:
    coordinate-pair
    | coordinate-pair ',' lineto-argument-sequence
    ;
closepath:
    'Z' | 'z';
lineto:
    'L' lineto-argument-sequence
    | 'l'  lineto-argument-sequence
    ;
lineto-argument-sequence:
    coordinate-pair
    | coordinate-pair ',' lineto-argument-sequence;
horizontal-lineto:
    'H' horizontal-lineto-argument-sequence
    | 'h'  horizontal-lineto-argument-sequence
    ;
horizontal-lineto-argument-sequence:
    coordinate
    | coordinate ',' horizontal-lineto-argument-sequence;
vertical-lineto:
    'V' vertical-lineto-argument-sequence
    | 'v'  vertical-lineto-argument-sequence
    ;
vertical-lineto-argument-sequence:
    coordinate
    | coordinate ',' vertical-lineto-argument-sequence;
curveto:
    'C' curveto-argument-sequence
    | 'c'  curveto-argument-sequence
    ;
curveto-argument-sequence:
    curveto-argument
    | curveto-argument-sequence ',' curveto-argument
    | curveto-argument-sequence  curveto-argument
    ;
curveto-argument:
    coordinate-pair ',' coordinate-pair ',' coordinate-pair
    | coordinate-pair  coordinate-pair  coordinate-pair
    ;
smooth-curveto:
    'S' smooth-curveto-argument-sequence
    | 's'  smooth-curveto-argument-sequence
    ;
smooth-curveto-argument-sequence:
    smooth-curveto-argument
    | smooth-curveto-argument ',' smooth-curveto-argument-sequence;
smooth-curveto-argument:
    coordinate-pair ',' coordinate-pair;
quadratic-bezier-curveto:
    'Q' quadratic-bezier-curveto-argument-sequence
    | 'q'  quadratic-bezier-curveto-argument-sequence
    ;
quadratic-bezier-curveto-argument-sequence:
    quadratic-bezier-curveto-argument
    | quadratic-bezier-curveto-argument ',' 
        quadratic-bezier-curveto-argument-sequence;
quadratic-bezier-curveto-argument:
    coordinate-pair ',' coordinate-pair;
smooth-quadratic-bezier-curveto:
    'T' smooth-quadratic-bezier-curveto-argument-sequence
    | 't'  smooth-quadratic-bezier-curveto-argument-sequence
    ;
smooth-quadratic-bezier-curveto-argument-sequence:
    coordinate-pair
    | smooth-quadratic-bezier-curveto-argument-sequence ',' coordinate-pair;
elliptical-arc:
    'A' elliptical-arc-argument-sequence
    | 'a' elliptical-arc-argument-sequence
    ;
elliptical-arc-argument-sequence:
    elliptical-arc-argument
    | elliptical-arc-argument-sequence ',' elliptical-arc-argument
    | elliptical-arc-argument-sequence  elliptical-arc-argument
    ;
elliptical-arc-argument:
    NUMBER ',' NUMBER ',' 
        NUMBER ',' NUMBER ',' NUMBER ',' coordinate-pair;
coordinate-pair:
    coordinate ',' coordinate
    | coordinate  coordinate
    ;
coordinate:
    NUMBER;
%%

extern "C"
{
    int derror (const char *s)
    {
        std::cerr << s << std::endl;
        return 0;
    }
}
