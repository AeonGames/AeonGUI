# Copyright 2015,2023,2024 AeonGames, Rodrigo Hernandez
# Licensed under the terms of the Apache 2.0 License.
include(CMakeParseArguments)
function(gitclone)
	cmake_parse_arguments(
			ARG # prefix of output variables
			"" # list of names of the boolean arguments (only defined ones will be true)
			"REPO;PATH;TAG" # list of names of mono-valued arguments
			"PATCHES" # list of names of multi-valued arguments (output variables are lists)
			${ARGN} # arguments of the function to parse, here we take the all original ones
		)
	if(NOT IS_DIRECTORY "${ARG_PATH}")
		message(STATUS "Cloning ${ARG_REPO}, please wait")
		execute_process(COMMAND ${GIT_EXECUTABLE} clone --depth 1 ${ARG_REPO} ${ARG_PATH} WORKING_DIRECTORY "${CMAKE_SOURCE_DIR}" RESULT_VARIABLE git_result OUTPUT_VARIABLE git_output ERROR_VARIABLE git_output)
		if(NOT git_result EQUAL 0)
			MESSAGE(FATAL_ERROR "Cloning ${ARG_REPO} failed.\nResult: ${git_result}\nOutput: ${git_output}")
		endif(NOT git_result EQUAL 0)

		execute_process(COMMAND ${GIT_EXECUTABLE} fetch --depth 1 origin ${ARG_TAG} WORKING_DIRECTORY "${ARG_PATH}" RESULT_VARIABLE git_result OUTPUT_VARIABLE git_output ERROR_VARIABLE git_output)
		if(NOT git_result EQUAL 0)
			MESSAGE(FATAL_ERROR "Fetching ${ARG_TAG} failed.\nResult: ${git_result}\nOutput: ${git_output}")
		endif(NOT git_result EQUAL 0)

		execute_process(COMMAND ${GIT_EXECUTABLE} checkout FETCH_HEAD WORKING_DIRECTORY "${ARG_PATH}" RESULT_VARIABLE git_result OUTPUT_VARIABLE git_output ERROR_VARIABLE git_output)
		if(NOT git_result EQUAL 0)
			MESSAGE(FATAL_ERROR "Checkout ${FETCH_HEAD} failed.\nResult: ${git_result}\nOutput: ${git_output}")
		endif(NOT git_result EQUAL 0)
		foreach(PATCH ${ARG_PATCHES})
			message(STATUS "Applying patch ${PATCH}")
			execute_process(COMMAND ${GIT_EXECUTABLE} apply ${PATCH} WORKING_DIRECTORY "${ARG_PATH}" RESULT_VARIABLE git_result OUTPUT_VARIABLE git_output ERROR_VARIABLE git_output)
			if(NOT git_result EQUAL 0)
				MESSAGE(FATAL_ERROR "Failed to apply ${PATCH}.\nResult: ${git_result}\nOutput: ${git_output}")
			endif(NOT git_result EQUAL 0)
			endforeach()
	endif(NOT IS_DIRECTORY "${ARG_PATH}")
endfunction(gitclone)

function(download url filename)
	if(NOT EXISTS "${CMAKE_SOURCE_DIR}/${filename}")
		message(STATUS "Downloading ${url}")
		file(DOWNLOAD "${url}" "${CMAKE_SOURCE_DIR}/${filename}" STATUS download_status SHOW_PROGRESS)
		list(GET download_status 0 download_status_code)
		list(GET download_status 1 download_status_desc)
		if(download_status_code)
			file(REMOVE "${CMAKE_SOURCE_DIR}/${filename}")
			message(FATAL_ERROR "Download failed\nError Code: ${download_status_code}\nMessage: ${download_status_desc}")
		endif()
		message(STATUS "Done downloading ${filename}")
	endif()
endfunction(download url filename)

function(decompress filename directory)
	if(NOT IS_DIRECTORY "${CMAKE_SOURCE_DIR}/${directory}")
		message(STATUS "Extracting ${filename} into ${CMAKE_SOURCE_DIR}")
		execute_process(COMMAND ${CMAKE_COMMAND} -E tar xzvf "${CMAKE_SOURCE_DIR}/${filename}" WORKING_DIRECTORY "${CMAKE_SOURCE_DIR}" RESULT_VARIABLE extract_result)
		if(extract_result)
			message(FATAL_ERROR "Extracting Failed with error code: ${extract_result}")
		endif()
	endif()
endfunction(decompress filename directory)

function(decompress_into filename directory)
	if(NOT IS_DIRECTORY "${CMAKE_SOURCE_DIR}/${directory}")
		file(MAKE_DIRECTORY "${CMAKE_SOURCE_DIR}/${directory}")
		message(STATUS "Extracting ${filename} into ${CMAKE_SOURCE_DIR}/${directory}")
		execute_process(COMMAND ${CMAKE_COMMAND} -E tar xzvf "${CMAKE_SOURCE_DIR}/${filename}" WORKING_DIRECTORY "${CMAKE_SOURCE_DIR}/${directory}" RESULT_VARIABLE extract_result)
		if(extract_result)
			message(FATAL_ERROR "Extracting Failed with error code: ${extract_result}")
		endif()
	endif()
endfunction(decompress_into filename directory)
